apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'fabric-loom'
apply plugin: 'com.replaymod.preprocess'

int mcVersion = project.mcVersion

repositories {
    maven {
        url = 'https://maven.fabricmc.net/'
        name = 'Fabric'
    }
    mavenCentral()
    maven {
        url 'https://masa.dy.fi/maven'
    }
    maven {
        url 'https://maven.fallenbreath.me/releases'
    }
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url 'https://www.cursemaven.com'
    }
    maven {
        url 'https://maven.shedaniel.me/'
    }
    maven {
        url 'https://maven.terraformersmc.com/releases/'
    }
}

// https://github.com/FabricMC/fabric-loader/issues/783
configurations {
    modRuntimeOnly.exclude group: 'net.fabricmc', module: 'fabric-loader'
    modLocalRuntime.exclude group: 'net.fabricmc', module: 'fabric-loader'
    modCompileOnly.exclude group: 'com.github.2No2Name', module: 'McTester'
    modImplementation.exclude group: 'com.github.2No2Name', module: 'McTester'
}

dependencies {
    // loom
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
    // Add SLF4J for logging, which is not included by default in older versions
    modImplementation "org.slf4j:slf4j-api:1.7.32"

    // Optional client-only UI deps (config screen + ModMenu entry)
    modCompileOnly "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"
    modLocalRuntime "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"
    modCompileOnly "com.terraformersmc:modmenu:${project.mod_menu_version}"
    modLocalRuntime "com.terraformersmc:modmenu:${project.mod_menu_version}"
    // 自己的模组以来也写在这里
}

String MIXIN_CONFIG_PATH = 'spectate.mixins.json'
JavaVersion JAVA_COMPATIBILITY
if (mcVersion >= 12005) {
    JAVA_COMPATIBILITY = JavaVersion.VERSION_21
} else if (mcVersion >= 11800) {
    JAVA_COMPATIBILITY = JavaVersion.VERSION_17
} else if (mcVersion >= 11700) {
    JAVA_COMPATIBILITY = JavaVersion.VERSION_16
} else {
    JAVA_COMPATIBILITY = JavaVersion.VERSION_1_8
}
JavaVersion MIXIN_COMPATIBILITY_LEVEL = JAVA_COMPATIBILITY

loom {
    def commonVmArgs = ['-Dmixin.debug.export=true']
    runConfigs.configureEach {
        // Keep per-MC-version run directories so local test mods (e.g. Carpet) don't conflict.
        runDir "../../run/${project.name}"
        vmArgs commonVmArgs
        ideConfigGenerated true
    }

    runs {
        def auditVmArgs = [*commonVmArgs, '-Ddemomod.mixin_audit=true']
        serverMixinAudit {
            server()
            vmArgs auditVmArgs
            ideConfigGenerated false
        }
        clientMixinAudit {
            client()
            vmArgs auditVmArgs
            ideConfigGenerated false
        }
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    exclude("META-INF")
    archiveClassifier.set('shadow')
}

tasks.withType(com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar).configureEach {
    enableRelocation true
    relocationPrefix 'com.spectate.libs'
}

remapJar {
    dependsOn(shadowJar)
    mustRunAfter(shadowJar)
    inputFile = shadowJar.archiveFile
}

String modVersionSuffix = ''
// detect github action environment variables
// https://docs.github.com/en/actions/learn-github-actions/environment-variables#default-environment-variables
// 要使这段代码发挥作用，需要在Github Action的构建运行时在env中添加 BUILD_RELEASE: "true"
if (System.getenv("BUILD_RELEASE") != "true") {
    String buildNumber = System.getenv("BUILD_ID")
    modVersionSuffix += buildNumber != null ? ('+build.' + buildNumber) : '-SNAPSHOT'
}
String fullModVersion = project.mod_version + modVersionSuffix
String fullProjectVersion

// Example version values:
//   project.mod_version     1.0.3                      (the base mod version)
//   modVersionSuffix        +build.88                  (use github action build number if possible)
//   artifactVersionSuffix   -SNAPSHOT
//   fullModVersion          1.0.3+build.88             (the actual mod version to use in the mod)
//   fullProjectVersion      v1.0.3-mc1.15.2+build.88   (in build output jar name)
//   fullArtifactVersion     1.0.3-mc1.15.2-SNAPSHOT    (maven artifact version)

// 这部分可以自定义，前提是理解了代码内容
group = project.maven_group
if (System.getenv("JITPACK") == "true") {
    // move mc version into archivesBaseName, so jitpack will be able to organize archives from multiple subprojects correctly
    base.archivesName = project.archives_base_name + '-mc' + project.minecraft_version
    fullProjectVersion = 'v' + project.mod_version + modVersionSuffix
} else {
    base.archivesName = project.archives_base_name
    fullProjectVersion = 'v' + project.mod_version + '-mc' + project.minecraft_version + modVersionSuffix
}
version = fullProjectVersion

// See https://youtrack.jetbrains.com/issue/IDEA-296490
// if IDEA complains about "Cannot resolve resource filtering of MatchingCopyAction" and you want to know why
processResources {
    inputs.property "id", project.mod_id
    inputs.property "name", project.mod_name
    inputs.property "version", fullModVersion

    filesMatching("fabric.mod.json") {
        def valueMap = [
                "id"               : project.mod_id,
                "name"             : project.mod_name,
                "version"          : fullModVersion,
                "minecraft_version": project.minecraft_version,
                "loader_version"   : project.loader_version // 这里也可以换成视情况而定的最低版本
        ]

        expand valueMap
    }

    // 如果没启用mixin，那这里可以删掉
    filesMatching(MIXIN_CONFIG_PATH) {
        filter { s -> s.replace('{{COMPATIBILITY_LEVEL}}', "JAVA_${MIXIN_COMPATIBILITY_LEVEL.ordinal() + 1}") }
    }

    // Resource formats differ across some MC versions for post effects; inject tracked overrides.
    if (mcVersion == 12104 || mcVersion >= 12107) {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
        def versionedResourcePath = mcVersion == 12104
                ? "src/versioned-resources/1.21.4"
                : "src/versioned-resources/1.21.7"
        from(rootProject.file(versionedResourcePath))
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    if (JAVA_COMPATIBILITY <= JavaVersion.VERSION_1_8) {
        // suppressed "source/target value 8 is obsolete and will be removed in a future release"
        options.compilerArgs << '-Xlint:-options'
    }
}

java {
    sourceCompatibility = JAVA_COMPATIBILITY
    targetCompatibility = JAVA_COMPATIBILITY
}

jar {
    from(rootProject.file('LICENSE')) {
        rename { "${it}_${project.archives_base_name}" }
    }
}
